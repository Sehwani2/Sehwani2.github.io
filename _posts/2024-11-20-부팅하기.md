---
typora-root-url: ../
title: "Chapter 4: 부팅하기"
categories:
  - 임베디드 OS 개발 프로젝트
tags:
  - RTOS
  - Operating System
  - Embedded
toc: true
date: 2024-11-20  # 추가된 날짜
---

# 부팅하기

**부팅이란** 

- 시스템에 전원이 들어가서 모든 초기화 작업을 마치고 펌웨어가 대기(idle) 상태가 될 때까지를 말하거나, ARM 코어가 리셋 익셉션 핸들러를 모두 처리한 다음에 C언어 코드로 넘어가기 직전까지를 의미한다.

## 메모리 설계 

- 보통 임베디드 시스템은 메모리 구조가 복잡하지만, QEMU는 단순한 메모리 구조로 되어 있다. 

**실행 파일은 메모리를 크게 세 가지로 나누어 사용합니다.**

- `text 영역` : 코드가 있는 공간
- `data 영역` : 초기화한 전역 변수가 있는 공간
- `BSS 영역`   : 초기화하지 않은 전역 변수가 있는 공간 

**text, data, BSS 영역을 어떻게 배치해야 할까?**

- 임베디드 시스템에서 **속도가 빠르지만 용량이 작은 메모리**와 **속도가 느리지만 용량이 큰 메모리**가 있을 경우:

  - **text 영역**: 빠른 메모리에 배치
  - **data 영역**: 속도에 민감한 데이터만 빠른 메모리에, 나머지는 큰 메모리에 배치
  - **BSS 영역**: 큰 메모리에 배치

- 하지만 QEMU는 그런 구분이 없으므로 순서대로 배치하고, 메모리는 넉넉하게 할당한다. 

| 모드     | 크기   | 주소 범위                  |
|----------|--------|---------------------------|
| **USR, SYS** | 2MB   | `0x00100000 ~ 0x002FFFFF` |
| **SVC**      | 1MB   | `0x00300000 ~ 0x003FFFFF` |
| **IRQ**      | 1MB   | `0x00400000 ~ 0x004FFFFF` |
| **FIQ**      | 1MB   | `0x00500000 ~ 0x005FFFFF` |
| **ABT**      | 1MB   | `0x00600000 ~ 0x006FFFFF` |
| **UND**      | 1MB   | `0x00700000 ~ 0x007FFFFF` |

- 개별 동작 모드마다 각 1MB 할당.
- USR,SYS모드는 메모리 공간과 레지스터를 공유하므로 하나로 묶었고 기본 동작 모드로 사용 될 것이므로 2MB 할당. 
- RTOS에서 동작하는 각 태스크는 **1MB의 스택 영역**을 할당받습니다. 나빌로스(Navilos)의 경우, 최대 태스크 수는 **64개**로, 총 **64MB의 스택 영역**이 필요합니다.
- 전역 변수용으로 1MB 할당, 그리고 남는 공간은 동적 할당 메모리용.

### 메모리 설계

| 영역             | 주소 범위                  | 크기    |
|------------------|---------------------------|---------|
| **Text 영역**     | `0x00000000 ~ 0x000FFFFF` | 1MB     |
| **USR/SYS 모드 스택** | `0x00100000 ~ 0x001FFFFF` | 1MB     |
| **SVC 모드 스택** | `0x00200000 ~ 0x002FFFFF` | 1MB     |
| **IRQ 모드 스택** | `0x00300000 ~ 0x003FFFFF` | 1MB     |
| **FIQ 모드 스택** | `0x00400000 ~ 0x004FFFFF` | 1MB     |
| **ABT 모드 스택** | `0x00500000 ~ 0x005FFFFF` | 1MB     |
| **UND 모드 스택** | `0x00600000 ~ 0x006FFFFF` | 1MB     |
| **태스크 스택 영역** | `0x00800000 ~ 0x047FFFFF` | 64MB    |
| **전역 변수 영역** | `0x04800000 ~ 0x048FFFFF` | 1MB     |
| **동적 할당 영역** | `0x04900000 ~ 0x07FFFFFF` | 55MB    |



##  익셉션 벡터 테이블 만들기

**수정된 Entry.S**

```assembly
	.global vertor_end

	vector_start:
	 LDR	PC, reset_handler_addr
	 LDR	PC, undef_handler_addr
	 LDR	PC, svc_handler_addr
	 LDR	PC, pftch_abt_handler_addr
	 LDR	PC, data_abt_handler_addr
	 B	.
	 LDR	PC, irq_handler_addr
	 LDR	PC, fiq_handler_addr
	 
	 reset_handler_addr:	 .word reset_handler
	 undef_handler_addr:	 .word dummy_handler
	 svc_handler_addr:	 .word dummy_handler
	 pftch_abt_handler_addr: .word dummy_handler
	 data_abt_handler_addr:  .word dummy_handler
	 irq_handler_addr:	 .word dummy_handler
	 fiq_handler_addr:	 .word dummy_handler
	 
	vector_end:
		
	reset_handler:
	 LDR	R0, = 0x10000000
	 LDR	R1, [R0]
	dummy_handler:
	 B .
.end
```

- 아직 아무 의미가 없는 코드. 

## 익셉션 핸들러 만들기 

이 장을 시작하기 위해서는 **ARM 아키텍처 기초 지식**이 필요하다.  나중에 시간이 있으면 따로 정리해 보겠다.



가장 먼저 만들어야 할 익셉션 핸들러는 리셋 익셉션 핸들러다. 

그렇다면 리셋 익셉션 핸들러가 해야 할 일은 무엇일까? 

- 바로 메모리 맵을 설정해 주는 작업이다. 

동작 모드별 스택이 모두 설정되고 나면 C언어 main() 함수로 진입할 것이다.

### 스택 만들기 

```c
#define INST_ADDR_START		0
#define USRSYS_STACK_START	0x00100000
#define SVC_STACK_START		0x00300000
#define IRQ_STACK_START		0x00400000
#define FIQ_STACK_START 	0x00500000
#define ABT_STACK_START		0x00600000
#define UND_STACK_START		0x00700000
#define TASK_STACK_START	0x00800000
#define GLOBAL_ADDR_START	0x04800000
#define DALLOC_ADDR_START	0x04900000

#define INST_MEM_SIZE		(USRSYS_STACK_START - INST_ADDR_START)
#define USRSYS_STACK_SIZE	(SVC_STACK_START - USRSYS_STACK_START)
#define SVC_STACK_SIZE		(IRQ_STACK_START - SVC_STACK_START)
#define IRQ_STACK_SIZE		(FIQ_STACK_START - IRQ_STACK_START)
#define FIQ_STACK_SIZE		(ABT_STACK_START - FIQ_STACK_START)
#define ABT_STACK_SIZE		(UND_STACK_START - ABT_STACK_START)
#define UND_STACK_SIZE		(TASK_STACK_START - UND_STACK_START)
#define TASK_STACK_SIZE		(GLOBAL_ADDR_START - TASK_STACK_START)
#define DALLOC_MEM_SIZE		(55 * 1024 * 1024)

#define USRSYS_STACK_TOP	(USRSYS_STACK_START + USRSYS_STACK_SIZE - 4)
#define SVC_STACK_TOP		(SVC_STACK_START + SVC_STACK_SIZE - 4)
#define IRQ_STACK_TOP		(IRQ_STACK_START + IRQ_STACK_SIZE - 4)
#define FIQ_STACK_TOP		(FIQ_STACK_START + FIQ_STACK_SIZE - 4)
#define ABT_STACK_TOP		(ABT_STACK_START + ABT_STACK_SIZE - 4)
#define UND_STACK_TOP		(UND_STACK_START + UND_STACK_SIZE - 4)
```

**`MemoryMap.h`**

- 위에서 설계한 메모리 맵을 C언어 코드로 표현한 코드 
- 각 동작 모드의 스택 시작 메모리 주소와 스택 크기 그리고 스택 꼭대기 메모리 주소를 정의 

```c
#define ARM_MODE_BIT_USR 0x10
#define ARM_MODE_BIT_FIQ 0x11
#define ARM_MODE_BIT_IRQ 0x12
#define ARM_MODE_BIT_SVC 0x13
#define ARM_MODE_BIT_ABT 0x17
#define ARM_MODE_BIT_UND 0x1B
#define ARM_MODE_BIT_SYS 0x1F
#define ARM_MODE_BIT_MON 0x16
```

**`ARMv7AR.h`**

- ARM의 `CPSR(Current Program Status Register)`에 값을 설정하여 동작 모드를 바꿀 수 있는 값을 정의한다. 

```assembly
#include "ARMv7AR.h"			//
#include "MemoryMap.h"			// include를 사용해 헤더 파일 포함한다.

.text
	.code 32

	.global vector_start
	.global vertor_end

	vector_start:
	 LDR	PC, reset_handler_addr
	 LDR	PC, undef_handler_addr
	 LDR	PC, svc_handler_addr
	 LDR	PC, pftch_abt_handler_addr
	 LDR	PC, data_abt_handler_addr
	 B	.
	 LDR	PC, irq_handler_addr
	 LDR	PC, fiq_handler_addr

	 reset_handler_addr:	 .word reset_handler
	 undef_handler_addr:	 .word dummy_handler
	 svc_handler_addr:	 	 .word dummy_handler
	 pftch_abt_handler_addr: .word dummy_handler
	 data_abt_handler_addr:  .word dummy_handler
	 irq_handler_addr:	 	 .word dummy_handler
	 fiq_handler_addr:	 	 .word dummy_handler
	vector_end:

	reset_handler:
	 MRS r0, cpsr
	 BIC r1, r0, #0x1F
	 ORR r1, r1, #ARM_MODE_BIT_SVC
	 MSR cpsr, r1
 	 LDR sp, =SVC_STACK_TOP

	 MRS r0, cpsr
	 BIC r1, r0, #0x1F
 	 ORR r1, r1, #ARM_MODE_BIT_IRQ
 	 MSR cpsr, r1
 	 LDR sp, =IRQ_STACK_TOP

	 MRS r0, cpsr
	 BIC r1, r0, #0x1F
	 ORR r1, r1, #ARM_MODE_BIT_FIQ
	 MSR cpsr, r1
	 LDR sp, =FIQ_STACK_TOP

	 MRS r0, cpsr
	 BIC r1, r0, #0x1F
	 ORR r1, r1, #ARM_MODE_BIT_ABT
	 MSR cpsr, r1
 	 LDR sp, =ABT_STACK_TOP

	 MRS r0, cpsr
	 BIC r1, r0, #0x1F
	 ORR r1, r1, #ARM_MODE_BIT_UND
	 MSR cpsr, r1
	 LDR sp, =UND_STACK_TOP

	 MRS r0, cpsr
	 BIC r1, r0, #0x1F
	 ORR r1, r1, #ARM_MODE_BIT_SYS
	 MSR cpsr, r1
	 LDR sp, =USRSYS_STACK_TOP

	dummy_handler:
	 B .
.end
```

**`동작 모드 스택 초기화 리셋 익셉션 핸들러 Entry.S`**

```assembly
 1 MRS r0, cpsr
 2 BIC r1, r0, #0x1F
 3 ORR r1, r1, #동작 모드
 4 MSR cpsr, r1
 5 LDR sp, =스택 꼭대기 메모리 주소 
```

1. 현재 cpsr 값을 r0에 저장 
2. 기존의 동작 모드 값을 제거
3. ARM의 동작 모드 변경 
4. 수정된 **r1** 값을 다시 CPSR에 저장하여 동작 모드를 변경
5. 현재 모드의 스택 포인터(`sp`)를 설정하는 명령, 스택은 높은 주소에서 낮은 주소로 자라나기 때문에 꼭대기 메모리 주소를 넣는다.

  



![image-20241120202700934](/images/2024-11-20-부팅하기/image-20241120202700934.png)

![image-20241120202930292](/images/2024-11-20-부팅하기/image-20241120202930292.png)

Makefile 수정 후 GDB 연결 후 확인하면 볼 수 있다. 



## 메인으로 진입하기 

![image-20241120203144360](/images/2024-11-20-부팅하기/image-20241120203144360.png)

- main() 함수로 진입 하는 건 간단하다 `Entry.S` 파일 마지막에 `BL main`으로 진입 할 수 있다. 

 ```c
 #include <stdint.h>
 
 void main(void)
 {
 	uint32_t* dummyAddr = (uint32_t*)(1024*1024*100);
     *dummyAddr = sizeof(long);
 }
 ```

- `boot` 디렉토리에  `Main.c` 파일을 생성하고 c언어 코드 동작 확인을 위한 코드 작성 

- 이제 `Makefile`을 수정해서 `boot/Main.c`를 포함하면서 c 파일을 컴파일해서 오브젝트 파일을 생성하도록 작성하면 끝. 